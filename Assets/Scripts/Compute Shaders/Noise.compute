#pragma kernel CSMain
#pragma multi_compile _PERLIN _SIMPLEX _WORLEY

// What noise function will be used depends on what is defined in C#
#if defined(_PERLIN)
    #include "PerlinNoise.hlsl"
    #define SAMPLE_NOISE(pos) perlin(pos)
#elif defined(_SIMPLEX)
    #include "SimplexNoise.hlsl"
    #define SAMPLE_NOISE(pos) simplex(pos)
#elif defined(_WORLEY)
    #include "WorleyNoise.hlsl"
    #define SAMPLE_NOISE(pos) worley(pos)
#else
    #error "No noise type specified"
#endif

struct OctaveParams
{
    float2 offset;
    float frequency;
    float amplitude;
};

// Buffers
RWStructuredBuffer<float> _HeightMapBuffer; 
StructuredBuffer<OctaveParams> _OctaveBuffer;

// Global Variables
float2 midPoint; // To make scale happen from the center.
uint mapWidth;
uint octaves;
float scaleFactor;
float warpStrength; 
float warpFrequency;

[numthreads(16,16,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    // This vertex doesn't exist, assumes n*n map
    if (id.x >= mapWidth || id.y >= mapWidth) return;

    // What vertex this point represents; Starts from center instead of corner
    float2 pos = (id.xy - midPoint) / scaleFactor; // How zoomed in
    uint index = id.y * mapWidth + id.x;
    float height = 0;

    // DisplacementScale makes it so that strength doesn't directly affect the final vertical offset
    // regardless of where the warpFrequency (it was just a vertical offset value when freq was 0)
    float displacementScale = warpStrength * warpFrequency;
    float2 warpNoisePos = pos * warpFrequency;

    // warp offset is calculated, the main goal of this is to slightly offset where we're sampling from
    // this helps to avoid artifacts in the terrain when the scale is too large. This is especially true for perlin noise.
    float2 warp = displacementScale * float2(
        SAMPLE_NOISE(warpNoisePos), // dx
        SAMPLE_NOISE(warpNoisePos + float2(100, 100)) // dy (offset seed)
    );
    pos += warp;
    
    [unroll(10)] // Will get out gracefully if less than 10 octaves as well
    for (uint i = 0; i < octaves; i++)
    {
        // Pick a new position to sample from each octave
        float2 newPos = (pos - _OctaveBuffer[i].offset) * _OctaveBuffer[i].frequency;

        // Add a scaled value based on amplitude.
        // Range of returned values is insignificant because they will be normalized later
        height += SAMPLE_NOISE(newPos) * _OctaveBuffer[i].amplitude;
    }

    _HeightMapBuffer[index] = height;
}
