// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#define MAX_UINT 4294967295
#define EPSILON 1e-6

// The following code makes two assumptions
// 1. This code is Single Execution (Not runtime code, or running every frame)
// 2. The terrain is infinite, rather than being an island or suddenly stopping at the edge of the map

// Buffers
RWStructuredBuffer<float> _HeightBuffer; // working with floats is easier and more efficient than float3's

// Variables
uint numVertices;
uint radius;
uint _seed;
int2 dimension; // {mapWidth, mapHeight} Refers to number of faces, not vertices 
float inertia;
float maxSediment;
float depositionRate;
float evaporationRate;
float softness;
float gravity;
float minSlope;

// Precomputed constants
static const float2 dimensionF = float2(dimension);
static const float verticesPerRow = dimension.x + 1;
static const float fRadius = (float)radius + .001; // Needs to prevent division by 0

float3 GetGradientAndHeight(float2 pos)
{
    // Position clamping to a valid edge implies there is terrain outside the edge of the map that is a same height
    // Get row/column of top left index of (square) face
    int col = clamp((int)pos.x, 0, dimension.x);
    int row = clamp((int)pos.y, 0, dimension.y);
    const int index = row * verticesPerRow + col;

    // Get height of all vertices of square
    const float nw = _HeightBuffer[index];                       // Top Left
    const float ne = _HeightBuffer[index + 1];                   // Top Right
    const float sw = _HeightBuffer[index + verticesPerRow];      // Bottom Left
    const float se = _HeightBuffer[index + verticesPerRow + 1];  // Bottom Right
    
    // Get sub-coordinates of drop in quad
    // Top left is (0,0)
    const float u = frac(pos.x);
    const float v = frac(pos.y);
    const float invU = 1.0f - u;
    const float invV = 1.0f - v;

    // Bilinear Interpolation of heights to get gradient direction and height
    const float angleX = lerp(ne-nw, se-sw, v);
    const float angleY = lerp(sw-nw, se-ne, u);
    const float height = nw * invU * invV + ne * u * invV + sw * invU * v + se * u * v;

    return float3(angleX, angleY, height);
}


// Simulate a raindrop picking up and depositing sediment
void SimulateDrop(int x, int y) 
{
    // Local drop variables
    // Position is on a grid between mapWidth + 1 and mapHeight + 1
    // Raindrop will always start directly in the middle of a face, and at least 0.5 away from the edge of the world
    float2 position = float2(x + 0.5f, y + 0.5f);
    float velocity = 1.0f;
    float2 dir = 0.0f;
    float volume = 1.0f;
    float sediment = 0.0f;
    
    
    [loop] // Max Steps to ensure drop doesn't move endlessly
    for (int i = 0; i < 64; i++)
    {
        // COULD EXCLUDE BORDERS HERE IF NECESSARY
        // We shouldn't continue if the starting position of the drop for this step is already off the map
        // Drops exactly on the right or bottom border should also be considered off because the gradient would consider them at an out-of-bounds cell
        if (any(position < 0.0f)) return;
        if (any(position >= dimensionF)) return;
        
        // Get location variables
        const float3 g = GetGradientAndHeight(position);
        const float oldHeight = g.z;
        

        // Update flow direction based on gradient
        // Inertia determines how much gradient(terrain directed angle) is taken into account
        dir = dir * inertia - g.xy * (1 - inertia);
        if (dot(dir, dir) < EPSILON) return;
        dir = normalize(dir) * .5f; // Drop moves fixed distance of .5f, so erosion is evenly distributed

        // Find next position
        const float2 newPos = position + dir;
        const float newHeight = GetGradientAndHeight(newPos).z;
        const float heightDif = newHeight - oldHeight;

        // Speed and size of the drop determine how much sediment it can hold
        const float dropCapacity = max(-heightDif, minSlope) * velocity * volume * maxSediment;
        
        // is drop ascending or full
        if (heightDif > 0.0f || sediment > dropCapacity)
        {
            float depositAmount;
            
            if (heightDif > 0) // Drop was ascending
            {
                // Either completely fill pit at oldPos or deposit all sediment there
                depositAmount = min(sediment, heightDif);
            }
            else
            {
                // A percentage of the surplus sediment is dropped
                depositAmount = (sediment - dropCapacity) * depositionRate;    
            }

            // Remove the lost sediment from the drop
            sediment -= depositAmount;

            // Get coordinates of drop in (square) face.
            // Top left is (0,0)
            const float u = frac(position.x);
            const float v = frac(position.y);
            const float invU = 1.0f - u;
            const float invV = 1.0f - v;
            const int index = (int)position.y * verticesPerRow + (int)position.x;
            
            // Add it to the vertices of the (square) face the drop is on
            // Weight for each vertex is bilinearly interpolated based on drops position in the face
            _HeightBuffer[index] += max(0, depositAmount * invU * invV);                // Top Left
            _HeightBuffer[index + 1] += max(0, depositAmount * u * invV);               // Top Right
            _HeightBuffer[index + verticesPerRow] += max(0, depositAmount * invU * v);  // Bottom Left
            _HeightBuffer[index + verticesPerRow + 1] += max(0, depositAmount * u * v); // Bottom Right
        }
        else // downhill and the drop can hold more sediment
        {
            // Amount of sediment to take won't be greater than the height difference
            float erosionAmount = min((dropCapacity - sediment) * softness, -heightDif);

            // Reduce erosion based on how close we are to the map edge, starting at a radius length away
            // The point of this is to prevent massive ravines forming at the edge of the map as a result of drop pooling
            const float edgeX = min(position.x, dimensionF.x - position.x);
            const float edgeY = min(position.y, dimensionF.y - position.y);
            const float edgeFactor = clamp(min(edgeX, edgeY) / fRadius, 0.0f, 1.0f);
            erosionAmount *= edgeFactor;

            // We take full amount as we can't track info for out-of-bounds vertices
            sediment += erosionAmount; 

            // We initially include invalid vertices so that weighting isn't heavy on ones inside the map
            // This should help give the illusion that the terrain continues outside the map
            int minC = (int)floor(position.x - fRadius);
            int maxC = (int)ceil(position.x + fRadius);
            int minR = (int)floor(position.y - fRadius);
            int maxR = (int)ceil(position.y + fRadius);

            float norm = 0.0f;
            int r,c;
            
            // First pass, which finds the normalization factor
            for (r = minR; r <= maxR; r++)
            {
                for (c = minC; c <= maxC; c++)
                {
                    const float d = distance(position, float2(c, r));

                    if (d <= fRadius)
                    {
                        norm += radius - d;
                    }
                }
            }

            // We're now only checking valid vertices
            minC = max(0, minC);
            maxC = min(dimension.x, maxC);
            minR = max(0, minR);
            maxR = min(dimension.y, maxR);
            const float invNorm = 1.0f / norm;

            // Second pass actually lowers terrain based on erosionAmount and weight of points
            for (r = minR; r <= maxR; r++)
            {
                for (c = minC; c <= maxC; c++)
                {
                    const int index = r * verticesPerRow + c;
                    const float d = length(position - float2(c, r));

                    if (d <= fRadius)
                    {
                        // Weight is normalized to be between 0 and 1 then that percentage is multiplied by the total amount eroded
                        // Because drop is on the map, vertices will be within the radius, so norm is never 0
                        const float weight = max(0, (fRadius - d) * invNorm);

                        // Erode the terrain by the proper amount. Min prevents vertices from going below 0
                        _HeightBuffer[index] -= min(erosionAmount * weight, _HeightBuffer[index]);
                    }
                }
            }
        }

        // Velocity is adjusted depending upon the slope and gravity, max prevents negative square root
        velocity = sqrt(max(0.0f, velocity * velocity + heightDif * -gravity));

        // Update Position
        position = newPos;
        volume *= 1.0f - evaporationRate;
    }
}

// We're creating the random drop location at runtime in the gpu as an optimization over precomputing 300k+ drops on the cpu
// PCG Hash is a well known hash function. Implementation from here: https://www.reedbeta.com/blog/hash-functions-for-gpu-rendering/
uint PCG(uint input)
{
    uint state = input * 747796405u + 2891336453u;
    uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    return (word >> 22u) ^ word;
}

[numthreads(64,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numVertices) return; // Don't need to iterate on

    uint hashVal = PCG(_seed + id.x);

    // Put seed within terrain boundaries, excluding right and bottom edges
    int x = hashVal % dimension.x; // Upper bits for x position
    int y = (hashVal >> 16) % dimension.y; // Lower Bits for y position

    // Converting to an index and starting the drop at that index
    SimulateDrop(x, y);
}
